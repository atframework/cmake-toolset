include_guard(GLOBAL)

if(NOT ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_INC_DIR)
  # =========== third party rapidjson ==================
  set(ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_VERSION master)
  set(ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_REPO_DIR
      "${PROJECT_THIRD_PARTY_PACKAGE_DIR}/rapidjson-${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_VERSION}"
  )

  if(Rapidjson_ROOT)
    set(RAPIDJSON_ROOT ${Rapidjson_ROOT})
  endif()

  find_package(Rapidjson QUIET)
  if(NOT Rapidjson_FOUND)
    project_git_clone_repository(
      URL
      "https://github.com/Tencent/rapidjson.git"
      REPO_DIRECTORY
      ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_REPO_DIR}
      DEPTH
      200
      BRANCH
      ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_VERSION}
      WORKING_DIRECTORY
      ${CMAKE_SOURCE_DIR})

    set(Rapidjson_ROOT ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_REPO_DIR})
    set(RAPIDJSON_ROOT ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_REPO_DIR})
    set(ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_ROOT_DIR
        ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_REPO_DIR})
    find_package(Rapidjson)
  else()
    set(ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_ROOT_DIR ${RAPIDJSON_ROOT})
  endif()

  if(Rapidjson_FOUND)
    echowithcolor(COLOR GREEN "-- Dependency: rapidjson found.(${Rapidjson_INCLUDE_DIRS})")
  else()
    echowithcolor(COLOR RED "-- Dependency: rapidjson is required")
    message(FATAL_ERROR "rapidjson not found")
  endif()

  set(ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_INC_DIR ${Rapidjson_INCLUDE_DIRS})
  list(APPEND ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_PUBLIC_INCLUDE_DIRS
       ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_INC_DIR})
else()
  list(APPEND ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_PUBLIC_INCLUDE_DIRS
       ${ATFRAMEWORK_CMAKE_TOOLSET_THIRD_PARTY_RAPIDJSON_INC_DIR})
endif()
